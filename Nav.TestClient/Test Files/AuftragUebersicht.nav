[namespaceprefix Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Auftrag]

[using System.Collections.Generic]
[using Pharmatechnik.Apotheke.XTplus.Common.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Common.WFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.NavigationEngine.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.NavigationEngine.WFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.Core.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.Core.WFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.Amounts.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Framework.Util]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Common.WFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Common.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaCommon.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaCommon.WFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaKasse.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaKasse.WFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Firmenstamm.IBOL]
// Kommentar
[using Pharmatechnik.Apotheke.XTplus.Umlagerung.IBOL]

[using Pharmatechnik.Apotheke.XTplus.Verkauf.ALL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.WFL] 

[using Pharmatechnik.Apotheke.XTplus.Verkauf.Abholungen.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.Abholungen.WFL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.Abholungen.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.UebergreifenderVerkauf.IBOL]
 
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Auftrag.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaCommon.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Bestellwesen.Schnittstellen.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Lieferschein.IBOL]
[using Pharmatechnik.Apotheke.XTplus.KundeArztverwaltung.IBOL]
[using Pharmatechnik.Apotheke.XTplus.KundeArztverwaltung.Kundenverwaltung.WFL]
[using Pharmatechnik.Apotheke.XTplus.KundeArztverwaltung.Kundenverwaltung.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Kontaktverwaltung.WFL]
[using Pharmatechnik.Apotheke.XTplus.Kontaktverwaltung.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Kontaktverwaltung.IWFL]
[using Pharmatechnik.Apotheke.XTplus.Faktura.FakturaUebersicht.Common.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Kontaktverwaltung.Common.WFL]
[using Pharmatechnik.Apotheke.XTplus.Mitarbeiterverwaltung.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Artikelstamm.MI.Verkauf.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Lagerbestand.MI.IBOL]
[using Pharmatechnik.Apotheke.XTplus.FakturaStammdaten.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Common.IBOL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.Rezeptkontrolle.IBOL] 
[using Pharmatechnik.Apotheke.XTplus.Botendienst.MI.IBOL] 
[using Pharmatechnik.Apotheke.XTplus.Report.MI.WFL]
[using Pharmatechnik.Apotheke.XTplus.Verkauf.MI.WFL]

taskref "MessageBoxes.nav";
taskref "..\..\FakturaKasse\AuftragKasse.nav"
taskref ".\ForderungsgrundlageSuche.nav"
taskref ".\AuftragLoeschen.nav"
taskref ".\AuftragStornieren.nav"
taskref "..\..\FakturaCommon\AuftragInfo.nav"
taskref "..\Common\BelegHistorie.nav"
taskref "..\..\FakturaKasse\AuftragAnlegen.nav"
taskref "..\..\FakturaKasse\LieferscheinErstellen.nav"
taskref "..\..\..\XTplus.Verkauf\Abholungen\BestellscheinAufloesen.nav"
taskref "..\..\..\XTplus.Verkauf\Abholungen\AbholscheinDrucken.nav"
taskref "..\..\..\XTplus.Verkauf\Abholungen\Ersatzartikel.nav";
taskref  "..\Lieferschein\SammelscheinErstellen.nav"
taskref "..\..\..\KundeArztverwaltung\Kundenverwaltung\Kundenverwaltung.nav"
taskref "..\..\..\KundeArztverwaltung\Common\Kundenliste.nav"
taskref "..\..\..\XTplus.Verkauf\FakturaKundenauswahlValidieren.nav"
taskref "AuftragDruckAuswahl.nav";
taskref "AuftragZusammenfassung.nav";
// Noch ein Kommentar
taskref Druckeinstellungen [namespaceprefix Pharmatechnik.Apotheke.XTplus.Report.MI]			
						   [result bool]
{
    init [params string bereich, string reportName];
    exit Exit;       
}

task AuftragUebersicht  [code "kjkjkj" "jgugj"]
                        [base StandardWFS<AuftragUebersichtTS> : IWFServiceBase, IBeginUebersichtBaseWFS]
                        [generateto "sdkjkjkjkj"]
                        [params AuftragUebersichtConverter auftragUebersichtConverter,                                           
								IFakturaCommonBS fakturaCommonBS,
								IAbholscheinBS abholscheinBS,
								IBestellungUebergebenV2BS bestellungUebergebenV2BS, 
								IAuftragBS auftragBS,
                                IEingangsRechnungBS eingangsRechnungBS,
								IFakturaUmlagerungBS fakturaUmlagerungBS,
								ISubjektOrtZeitFactory sozFactory,
								IKassenrabattBS kassenrabattBS, 
                                ILagerbestandBS lagerBestandBS, 
                                IFirmenstammBS firmenstammBS, 
                                ILieferscheindruckenKP lieferscheindruckenKP,
                                IRezeptkontrolleService rezeptkontrolleService,
                                ITourenBS tourenBS, 
								IBuchungServices buchungServices,
								IUebergreifenderVerkaufBS uebergreifenderVerkaufBS,
								IMitarbeiterverwaltungBS mitarbeiterBS,
                                ForderungsgrundlageAdapterFactory forderungsgrundlageAdapterFactory]                               
						[result UebersichtTaskResult x]
{
	// inherited
	init ss [abstractmethod] [params IUebersichtBaseParams<Foo<D>>[] initParams];
	exit    Fertig;	
	exit    SwitchUebersicht;
    
	// Specific
	view   AuftragUebersicht;	
    dialog TestDialog;
	choice xxx;
	choice auftragAngelegt;
	choice UebersichtWechseln;
	choice AbholscheinGescannt;
	choice KundenInfoNichtVorhanden;
	choice Choice_BestellscheinAufloesen;

    task    ForderungsgrundlageSuche foo [donotinject] [abstractmethod];
	task    AuftragLoeschen [abstractmethod];
    task    AuftragStornieren;
	task    AuftragInfo [donotinject];
	task    AuftragKasse;
    task    BelegHistorie;
    task    AuftragAnlegen;
    task    LieferscheinErstellen;
    task    AbholscheinDrucken AbholscheinDruckenOnLieferscheinErstellen;
    task    BestellscheinAufloesen;
    task    MessageBoxWith3Buttons MsgF12LieferscheinErstellen_NaUndLieferbareMenge;
    task    MessageBoxYesNo MsgF12LieferscheinErstellen_NurNachliefermenge;
	task    SammelscheinErstellen;
	task    Kundenverwaltung;
	task    Kundenliste;
	task    AuftragDruckAuswahl;
    task    Ersatzartikel;
	task    AuftragZusammenfassung;
    task    FakturaKundenauswahlValidieren F12_LieferscheinErstellen_KundenValidieren;
    task    Druckeinstellungen;
    task    MessageBoxYesNo MsgAktivierungRezeptInRezeptboxOderIstAbgerechnet;
    task    MessageBoxYesNo MsgStornierungRezeptInRezeptboxOderIstAbgerechnet;
    task    MessageBoxOk  MsgAktivierungMindestensEinLieferscheinBereitsAnBoteUebergeben;
    task    MessageBoxOk  MsgStornierungMindestensEinLieferscheinBereitsAnBoteUebergeben;
    task    MessageBoxOk  MsgStornierungEingangsrechnungFehlgeschlagen;
	task	MessageBoxYesNo MsgBox_UebergreifenderVerkaufBetroffen;
	task	MessageBoxYesNo MsgBox_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen;
	
    choice  Choice_BestellscheinAufgeloest;
	choice  Choice_F12_LieferscheineErstellen;
    choice  Choice_StartVariantenOnInit;
	choice  Choice_IstAuftragStornierbar;
	choice  Choice_IstAuftragAktivierbar;

	// Hinweis: Wenn möglich sollten eigentlich alle folgenden Abfragen in einen eigenen Workflow wie z. B. VerkaufBearbeiten oder VerkaufStornieren ausgelagert werden.
	choice  Choice_Aktivierung_RezeptInRezeptboxOderIstAbgerechnet;
	// choice  Choice_Stornierung_RezeptInRezeptboxOderIstAbgerechnet;				<--- wird nicht benötigt, weil diese Meldung bereits innerhalb von VerkaufStornieren.nav behandelt wird
	choice  Choice_Aktivierung_MindestensEinLieferscheinBereitsAnBoteUebergeben;
    choice  Choice_Stornierung_MindestensEinLieferscheinBereitsAnBoteUebergeben;
	choice  Choice_Aktivierung_UebergreifenderVerkaufBetroffen;
	choice  Choice_Aktivierung_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen;
    
        
	// ------------------------------------------------
	// Tab Switch
	// ------------------------------------------------
    AuftragUebersicht                       --> SwitchUebersicht on TabChangedEvent;

	// ------------------------------------------------
	// initialize
	// ------------------------------------------------
	init                                    --> UebersichtWechseln; 
    
    // ------------------------------------------------
	// F2 Suche
	// ------------------------------------------------
    AuftragUebersicht                       o-> ForderungsgrundlageSuche on F2_Suchen;
    ForderungsgrundlageSuche:OK             *-> AuftragUebersicht;
    ForderungsgrundlageSuche:Abbrechen      --> AuftragUebersicht;
	
	// ------------------------------------------------
	// F3 Neu
	// ------------------------------------------------
    AuftragUebersicht                       o-> AuftragAnlegen          on "F3_Neu";
    AuftragUebersicht                       o-> AuftragAnlegen          on StrgF3Neu;
    AuftragAnlegen:Beenden                  --> Choice_BestellscheinAufloesen;

    Choice_BestellscheinAufloesen           o-> BestellscheinAufloesen  if "Bestellschein in Offene Posten zum Aufzulösen ausgewählt";
    Choice_BestellscheinAufloesen           --> UebersichtWechseln      if "Ohne Bestellscheinauflösung";
	
    UebersichtWechseln                      --> SwitchUebersicht            if "Tab hat sich geändert";
    UebersichtWechseln                      --> Choice_StartVariantenOnInit if "Tab hat sich nicht geändert";
	

    Choice_StartVariantenOnInit             --> AuftragUebersicht       if "Standard";
    Choice_StartVariantenOnInit             --> AuftragUebersicht       if "BestellscheinAuflösen"; //manueller Concat auf BestellscheinAufloesen

	// ------------------------------------------------
	// F4 Löschen
	// ------------------------------------------------
    AuftragUebersicht                       o-> AuftragLoeschen on F4_Loeschen;
    AuftragLoeschen:Yes                     --> AuftragUebersicht;
    AuftragLoeschen:No                      --> AuftragUebersicht;
	
	// ------------------------------------------------
	// F5 Bearbeiten
	// ------------------------------------------------
	AuftragUebersicht							            --> Choice_Aktivierung_RezeptInRezeptboxOderIstAbgerechnet on F5_Bearbeiten;
	Choice_Aktivierung_RezeptInRezeptboxOderIstAbgerechnet	o-> MsgAktivierungRezeptInRezeptboxOderIstAbgerechnet						if "Auftrag enthält Rezept(e) in Rezeptbox oder Abgerechnet (RZM)";
	Choice_Aktivierung_RezeptInRezeptboxOderIstAbgerechnet	--> Choice_Aktivierung_MindestensEinLieferscheinBereitsAnBoteUebergeben 	if "weiter pruefen ob Aktivierung moeglich";
	
	MsgAktivierungRezeptInRezeptboxOderIstAbgerechnet:Yes	--> Choice_Aktivierung_MindestensEinLieferscheinBereitsAnBoteUebergeben;
	MsgAktivierungRezeptInRezeptboxOderIstAbgerechnet:No	--> AuftragUebersicht;
	
	Choice_Aktivierung_MindestensEinLieferscheinBereitsAnBoteUebergeben o-> MsgAktivierungMindestensEinLieferscheinBereitsAnBoteUebergeben if "Auftrag enthält per Bote gelieferte Lieferscheine (Botendienst)";
	Choice_Aktivierung_MindestensEinLieferscheinBereitsAnBoteUebergeben --> Choice_Aktivierung_UebergreifenderVerkaufBetroffen else;

	MsgAktivierungMindestensEinLieferscheinBereitsAnBoteUebergeben:Ok	--> Choice_Aktivierung_UebergreifenderVerkaufBetroffen;
	
	Choice_Aktivierung_UebergreifenderVerkaufBetroffen		o-> MsgBox_UebergreifenderVerkaufBetroffen if "Übergreifender Verkauf betroffen (Apo A oder Apo B)";
	Choice_Aktivierung_UebergreifenderVerkaufBetroffen		--> Choice_IstAuftragAktivierbar else;
	
	MsgBox_UebergreifenderVerkaufBetroffen:Yes				--> Choice_IstAuftragAktivierbar;
	MsgBox_UebergreifenderVerkaufBetroffen:No				--> AuftragUebersicht;

    Choice_IstAuftragAktivierbar                o-> AuftragKasse        if "Auftrag ist aktivierbar";
    Choice_IstAuftragAktivierbar                *-> AuftragUebersicht   if "Auftrag ist nicht aktivierbar";
	
    AuftragKasse:Beenden                        --> Choice_BestellscheinAufloesen;

	// ------------------------------------------------
	// F6 Stornieren
	// ------------------------------------------------
	AuftragUebersicht													--> Choice_Stornierung_MindestensEinLieferscheinBereitsAnBoteUebergeben on F6_Stornieren;
	Choice_Stornierung_MindestensEinLieferscheinBereitsAnBoteUebergeben	o-> MsgStornierungMindestensEinLieferscheinBereitsAnBoteUebergeben		if "Auftrag enthält per Bote gelieferte Lieferscheine (Botendienst)";
	Choice_Stornierung_MindestensEinLieferscheinBereitsAnBoteUebergeben	--> Choice_IstAuftragStornierbar										if "weiter pruefen ob Stornierung moeglich";
	 
	MsgStornierungMindestensEinLieferscheinBereitsAnBoteUebergeben:Ok	--> Choice_IstAuftragStornierbar;

	Choice_IstAuftragStornierbar			o-> AuftragStornieren if "Auftrag ist stornierbar";
	Choice_IstAuftragStornierbar			--> AuftragUebersicht if "Auftrag ist nicht stornierbar";
	Choice_IstAuftragStornierbar            o-> MsgStornierungEingangsrechnungFehlgeschlagen if "Eingangsrechnung konnte nicht storniert werden";      

    MsgStornierungEingangsrechnungFehlgeschlagen:Ok --> AuftragUebersicht;
	
	AuftragStornieren:Abgebrochen			--> AuftragUebersicht;
	AuftragStornieren:Durchgefuehrt			--> AuftragUebersicht;
	
	// ------------------------------------------------
	// F7 NL_Auflösen
	// ------------------------------------------------

    AuftragUebersicht                       o-> BestellscheinAufloesen  on F7_NLAufloesen;
    AuftragUebersicht                       --> AbholscheinGescannt     on DataScanned;
    AbholscheinGescannt                     o-> BestellscheinAufloesen  if "gescannter Code war Abholschein";
    AbholscheinGescannt                     --> AuftragUebersicht       if "gscannter Code war kein Abholschein";
	
    BestellscheinAufloesen:OK               --> Choice_BestellscheinAufgeloest;
    BestellscheinAufloesen:Abbrechen        --> Choice_BestellscheinAufgeloest;
	
    Choice_BestellscheinAufgeloest          --> AuftragUebersicht if "Aufgeloest";
    Choice_BestellscheinAufgeloest          --> Choice_Aktivierung_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen if "Auftrag zum Bearbeiten ausgewählt";
    Choice_BestellscheinAufgeloest          o-> LieferscheinErstellen if "Auftrag Weiterführen";

	Choice_Aktivierung_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen	o-> MsgBox_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen if "Übergreifender Verkauf betroffen";
	Choice_Aktivierung_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen	o-> Ersatzartikel else;

	MsgBox_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen:Yes			--> Ersatzartikel;
	MsgBox_UebergreifenderVerkaufBetroffenBeiF7NlAufloesen:No			--> AuftragUebersicht;

    Ersatzartikel:AlleArtikelGeprueft       o-> AuftragKasse if "Auftrag bearbeiten";
		
	// ------------------------------------------------
	// F8 Detailansicht
	// ------------------------------------------------
    AuftragUebersicht                       o-> AuftragZusammenfassung on F8_Detailansicht;
	AuftragZusammenfassung:Exit             --> AuftragUebersicht;

	// ------------------------------------------------
	// F9 Drucken
	// ------------------------------------------------
    AuftragUebersicht                       o-> AuftragDruckAuswahl on F9_Drucken;
    AuftragDruckAuswahl:OK                  --> AuftragUebersicht;
	
	AuftragUebersicht                       --> AuftragUebersicht on AuftragDetailsRowActivated;
	

	// ------------------------------------------------
	// F12_LieferscheineErstellen
    // STRG + F12 Vollständige Lieferscheine erstellen
	// ------------------------------------------------
    AuftragUebersicht                               o-> F12_LieferscheinErstellen_KundenValidieren on F12_LieferscheineErstellen;
    AuftragUebersicht                               o-> F12_LieferscheinErstellen_KundenValidieren on Strg_F12_NurKomplettLieferbareSubtotalsLiefern;

    F12_LieferscheinErstellen_KundenValidieren:Abgebrochen  --> AuftragUebersicht;
	F12_LieferscheinErstellen_KundenValidieren:Valid        --> Choice_F12_LieferscheineErstellen;
	
    Choice_F12_LieferscheineErstellen               o-> LieferscheinErstellen                            if "keine Nachliefermenge vorhanden";
    Choice_F12_LieferscheineErstellen               o-> MsgF12LieferscheinErstellen_NaUndLieferbareMenge if "Nachliefermenge und lieferbare Menge vorhanden";
    Choice_F12_LieferscheineErstellen               o-> MsgF12LieferscheinErstellen_NurNachliefermenge   if "Nachliefermenge und keine lieferbare Menge vorhanden ist";
	
	
    MsgF12LieferscheinErstellen_NaUndLieferbareMenge:Fct1   o-> LieferscheinErstellen do "Nachlieferung auflösen";
    MsgF12LieferscheinErstellen_NaUndLieferbareMenge:Fct2   o-> LieferscheinErstellen do "LS für lieferbare Mengen";
    MsgF12LieferscheinErstellen_NaUndLieferbareMenge:Fct3   --> AuftragUebersicht;

    MsgF12LieferscheinErstellen_NurNachliefermenge:Yes      o-> LieferscheinErstellen do "Nachlieferung auflösen";
    MsgF12LieferscheinErstellen_NurNachliefermenge:No       --> AuftragUebersicht;

    LieferscheinErstellen:Abbrechen                 --> AuftragUebersicht;
    LieferscheinErstellen:Durchgefuehrt             --> AuftragUebersicht;

	// ------------------------------------------------
	// Strg_F6_Infotext
	// ------------------------------------------------
    AuftragUebersicht                       o-> AuftragInfo on Strg_F6_Infotext;
    AuftragInfo:Beenden                     --> AuftragUebersicht;
    AuftragInfo:AuftragInfoUebernehmen      --> AuftragUebersicht;

    // ------------------------------------------------
	// Alt+F11 Historie
	// ------------------------------------------------
    AuftragUebersicht                       o-> BelegHistorie on AltF11_Historie;
    BelegHistorie:Fertig                    --> AuftragUebersicht;
	
	// ------------------------------------------------
	// Grid Events
	// ------------------------------------------------
    AuftragUebersicht                       --> AuftragUebersicht on OnSelectionChange;

    AuftragUebersicht                       --> AuftragUebersicht on AfterSortChange;
	
	
	// ------------------------------------------------
	// Sammelscheinerstellen
	// ------------------------------------------------
    AuftragUebersicht                           o-> SammelscheinErstellen on StrgF5Sammelscheine;
    
    
    SammelscheinErstellen:OK                    --> AuftragUebersicht;
    SammelscheinErstellen:Abbrechen             --> AuftragUebersicht;
    
    
	
	// ------------------------------------------------
	// F10 Kunden Info
	// ------------------------------------------------
	
    AuftragUebersicht               --> KundenInfoNichtVorhanden on F10KundenInfo;
    KundenInfoNichtVorhanden        o-> Kundenliste       if "kunde und AuftragVorhanden";
    KundenInfoNichtVorhanden        --> AuftragUebersicht if "Kunde oder Auftrag nicht vorhanden";
    Kundenliste:Exit                --> AuftragUebersicht;
			
	// ------------------------------------------------
	// Exit
	// Noch nicht implementiert
	// ------------------------------------------------
    xxx                             --> AuftragUebersicht;
    
	//--------------
    //Druckeinstellungen
    //--------------
    AuftragUebersicht               o-> Druckeinstellungen on Druckeinstellungen;
    Druckeinstellungen:Exit         --> AuftragUebersicht;
}
taskref ""; task Foo {
	init;
	exit Exit;

	init --> Exit;
}
//Foo